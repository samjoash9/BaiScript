<program> ::= <statement_list>


<statement_list> ::= <statement> <statement_list>
                   | ε


<statement> ::= <declaration> "!"
               | <assignment> "!"
               | <simple_expr> "!"
               | <printing> "!"
               | "!"


<printing> ::= "PRENT" <print_list>


<print_list> ::= <print_item> <print_list_prime>
                | ε

<print_list_prime> ::= "," <print_item> <print_list_prime>
                      | ε

<print_item> ::= <simple_expr>
                | <string_literal>


<declaration> ::= <datatype> <init_declarator_list>


<datatype> ::= "CHAROT"
              | "ENTEGER"
              | "KUAN"


<init_declarator_list> ::= <init_declarator>
                          | <init_declarator> "," <init_declarator_list>


<init_declarator> ::= <declarator>
                     | <declarator> "=" <simple_expr>


<declarator> ::= <identifier>
                | "(" <declarator> ")"


<assignment> ::= <identifier> <assign_op> <assignment>
                | <identifier> <assign_op> <simple_expr>


<assign_op> ::= "="
               | "+="
               | "-="
               | "/="
               | "*="


<simple_expr> ::= <add_expr>


<add_expr> ::= <add_expr> "+" <term>
              | <add_expr> "-" <term>
              | <term>


<term> ::= <term> "*" <factor>
          | <term> "/" <factor>
          | <factor>


<factor> ::= <unary>
            | <postfix>


<unary> ::= "+" <factor>
           | "-" <factor>
           | "++" <postfix>
           | "--" <postfix>


<postfix> ::= <primary> <postfix_opt>


<postfix_opt> ::= "++"
                 | "--"
                 | ε


<primary> ::= <identifier>
             | <int_literal>
             | <char_literal>
             | "(" <simple_expr> ")"


<identifier> ::= <ident_start> <ident_continue_seq>

<ident_start> ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J"
                 | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T"
                 | "U" | "V" | "W" | "X" | "Y" | "Z"
                 | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j"
                 | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t"
                 | "u" | "v" | "w" | "x" | "y" | "z"
                 | "_"

<ident_continue_seq> ::= <ident_continue> <ident_continue_seq>
                        | ε

<ident_continue> ::= <ident_start>
                    | "0" | "1" | "2" | "3" | "4"
                    | "5" | "6" | "7" | "8" | "9"


<int_literal> ::= <digit> <digit_seq>

<digit_seq> ::= <digit> <digit_seq>
               | ε

<digit> ::= "0" | "1" | "2" | "3" | "4"
          | "5" | "6" | "7" | "8" | "9"


<char_literal> ::= "'" <char_item> "'"

<char_item> ::= <printable>
               | <escape_sequence>


<string_literal> ::= "\"" <string_char_seq> "\"" <- is just  { " <string_char_seq> " }

<string_char_seq> ::= <string_char> <string_char_seq>
                    | ε

<string_char> ::= <printable>
                 | <escape_sequence>


<escape_sequence> ::= "\n"
                    | "\t"
                    | "\r"
                    | "\\"
                    | "\'"
                    | "\""
                    | "\0"

<printable> ::= " " | "!" | "\"" | "#" | "$" | "%" | "&"
                | "'" 
                | "(" | ")" | "*" | "+" | "," | "-" | "." | "/"
                | "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7"
                | "8" | "9"
                | ":" | ";" | "<" | "=" | ">" | "?"
                | "@"
                | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H"
                | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P"
                | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X"
                | "Y" | "Z"
                | "[" | "\\" | "]" | "^" | "_" | "`"
                | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h"
                | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p"
                | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x"
                | "y" | "z"
                | "{" | "|" | "}" | "~"

